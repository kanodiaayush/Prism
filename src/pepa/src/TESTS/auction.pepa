
%%% The Auction model by Hillston and Kloul
%%% as published in the paper Concurrency: Practice and Experience

su=9.0;
sd=9.0;
st=2.0;
s=1.0;
pr=1.2;
ppr=0.8;
qr=1.2;
qqr=0.8;
r=2.0;

Bidder       = (bid_in,su).WaitingBid + (preq_in, sd).WaitingPrice;
WaitingBid   = (forward_accept, T).Bidder + (forward_reject, T).Incorrect;
WaitingPrice = (forward_presp,T).Bidder;
Incorrect    = (preq_in, st).WaitingPrice;


Bidder_CN       = (bid'_in,su).WaitingBid_CN + (preq'_in, sd).WaitingPrice_CN;
WaitingBid_CN   = (forward_accept', T).Bidder_CN 
                 + (forward_reject', T).Incorrect_CN;
WaitingPrice_CN = (forward_presp',T).Bidder_CN;
Incorrect_CN    = (preq'_in, st).WaitingPrice_CN;


Node  = (bid_in, T).Node1 + (preq_in, T).Node2 
       + (accept_s, T).Node3
       + (reject_s, T).Node4
       + (presp_s, T).Node5;
Node1 = (forward_bid, s).Node 
       + (presp_s, T).Node6
       + (accept_s, T).Node7
       + (reject_s, T).Node8;
Node2 = (forward_preq, s).Node 
       + (presp_s, T).Node9
       + (accept_s, T).Node10
       + (reject_s, T).Node11;
Node3 =(forward_accept, s).Node;
Node4 =(forward_reject, s).Node;
Node5 =(forward_presp, s).Node;
Node6 =(forward_presp, s).Node1;
Node7 =(forward_accept, s).Node1; 
Node8 =(forward_reject, s).Node1;
Node9 =(forward_presp, s).Node2; 
Node10 =(forward_accept, s).Node2; 
Node11 =(forward_reject, s).Node2;

CNode  = (bid'_in,T).CNode1 + (preq'_in,T).CNode2 
        + (forward_accept_sc,T).CNode3
        + (forward_reject_sc,T).CNode4
        + (forward_presp_sc, T).CNode5;
CNode1 = (forward_bid_nc, s).CNode 
        + (forward_accept_sc,T).CNode6
        + (forward_reject_sc,T).CNode7
        + (forward_presp_sc, T).CNode8;
CNode2 = (forward_preq_nc, s).CNode 
        + (forward_accept_sc,T).CNode9
        + (forward_reject_sc,T).CNode10
        + (forward_presp_sc, T).CNode11;
CNode3 = (forward_accept', s).CNode;
CNode4 =(forward_reject', s).CNode;
CNode5 =(forward_presp', s).CNode;
CNode6 =(forward_accept', s).CNode1;
CNode7 =(forward_reject', s).CNode1;  
CNode8 =(forward_presp', s).CNode1;
CNode9 =(forward_accept', s).CNode2; 
CNode10 =(forward_reject', s).CNode2; 
CNode11 =(forward_presp', s).CNode2;


TNode  = (bid_in, T).TNode1 + (forward_bid_nc, T).TNode1 
                + (preq_in, T).TNode2 + (forward_preq_nc, T).TNode2
                + (accept_sc, T).TNode3 + (reject_sc, T).TNode4
                + (presp_sc,T).TNode5;
TNode1 = (forward_bid_cs, s).TNode
                + (presp_sc,T).TNode6
                + (accept_sc, T).TNode7
                + (reject_sc, T).TNode8;
TNode2 = (forward_preq_cs, s).TNode
                + (presp_sc,T).TNode9
                + (accept_sc, T).TNode10
                + (reject_sc, T).TNode11;
TNode3 = (forward_accept_sc,s).TNode + (forward_accept, s).TNode;
TNode4 = (forward_reject_sc,s).TNode + (forward_reject, s).TNode;
TNode5 = (forward_presp_sc,s).TNode  + (forward_presp, s).TNode;
TNode6 = (forward_presp_sc,s).TNode1 + (forward_presp, s).TNode1;
TNode7 = (forward_accept_sc,s).TNode1+ (forward_accept, s).TNode1;
TNode8 = (forward_reject_sc,s).TNode1+ (forward_reject, s).TNode1;
TNode9 = (forward_presp_sc,s).TNode2 + (forward_presp, s).TNode2;
TNode10= (forward_accept_sc,s).TNode2 + (forward_accept, s).TNode2;
TNode11= (forward_reject_sc,s).TNode2+ (forward_reject, s).TNode2;

Server  = (forward_bid,T).Server' + (forward_bid_cs,T).Server'' 
         + (forward_preq,T).Server1 + (forward_preq_cs, T).Server2;
Server' = (accept_s,pr).Server + (reject_s,ppr).Server;
Server''= (accept_sc,qr).Server + (reject_sc,qqr).Server;
Server1 = (presp_s,r).Server;
Server2 =  (presp_sc,r).Server;


((Server <accept_sc,reject_sc,presp_sc,forward_bid_cs,forward_preq_cs> 
((Bidder <> Bidder)
<bid_in,forward_accept,forward_reject,preq_in,forward_presp> TNode)) 
<forward_bid_nc,forward_accept_sc,forward_reject_sc,forward_preq_nc,
forward_presp_sc> (CNode <bid'_in,preq'_in,forward_accept',forward_reject', 
forward_presp'> (Bidder_CN <> Bidder_CN))) 
<accept_s,reject_s,presp_s,forward_preq,forward_bid> Node <bid_in,preq_in, forward_accept,forward_reject,forward_presp> 
(Bidder <> Bidder)
